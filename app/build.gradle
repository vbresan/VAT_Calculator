apply plugin: "com.android.application"

android {
    namespace "biz.binarysolutions.vatcalculator"
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "biz.binarysolutions.vatcalculator"
        minSdk 21
        targetSdk 35
        versionCode 11
        versionName "2.0.5"

        setProperty("archivesBaseName", "$applicationId-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt"
        }
    }

    flavorDimensions = ["ads"]
    productFlavors {
        withAds {
            dimension "ads"
        }
        withoutAds {
            dimension "ads"

            dependenciesInfo {
                includeInApk = false
                includeInBundle = false
            }
        }
    }
}

dependencies {

    implementation "androidx.preference:preference:1.2.1"
    implementation "com.google.android.material:material:1.12.0"

    // version 24.0.0+ requires min SDK 23
    withAdsImplementation "com.google.android.gms:play-services-ads:23.6.0"
    withAdsImplementation project(":nativetemplates")
}

